#
# =============================================================================
# EARLY SETUP
# =============================================================================

# Auto-attach to tmux via sesh in Ghostty or Windows Terminal
if [[ -z "$TMUX" ]] && [[ -n "$GHOSTTY_RESOURCES_DIR" || -n "$WT_SESSION" ]]; then
    if tmux list-sessions &>/dev/null; then
        # Sessions exist - show picker
        exec sesh connect "$(
            sesh list --icons | fzf \
                --no-sort --ansi --border-label ' sesh ' --prompt 'âš¡  ' \
                --reverse \
                --footer '  ^a all ^t tmux ^g configs ^x zoxide ^d tmux kill ^f find' \
                --bind 'tab:down,btab:up' \
                --bind 'ctrl-a:change-prompt(âš¡  )+reload(sesh list --icons)' \
                --bind 'ctrl-t:change-prompt(ðŸªŸ  )+reload(sesh list -t --icons)' \
                --bind 'ctrl-g:change-prompt(âš™ï¸  )+reload(sesh list -c --icons)' \
                --bind 'ctrl-x:change-prompt(ðŸ“  )+reload(sesh list -z --icons)' \
                --bind 'ctrl-f:change-prompt(ðŸ”Ž  )+reload(fd -H -d 2 -t d -E .Trash . ~)' \
                --bind 'ctrl-d:execute(tmux kill-session -t {2..})+change-prompt(âš¡  )+reload(sesh list --icons)' \
                --preview-window 'right:55%' \
                --preview 'sesh preview {}'
        )"
    else
        # No sessions - ask user what to do
        choice=$(printf "ðŸªŸ  Create new tmux session\nðŸš  Stay in shell (no tmux)" | fzf \
            --prompt "No tmux sessions: " --reverse --height=4 --no-info)
        case "$choice" in
            *"Create new"*) exec tmux ;;
        esac
    fi
fi

if [[ "$PATH" == *"github.copilot-chat"* ]]; then
    PS1='$ '
    local si_script="$HOME/.vscode-server-insiders/bin/$(ls -t ~/.vscode-server-insiders/bin 2>/dev/null | head -1)/out/vs/workbench/contrib/terminal/common/scripts/shellIntegration-rc.zsh"
    [[ -f "$si_script" ]] && . "$si_script"
    return
fi

# fpath modifications FIRST (beforeO compinit)
export fpath=($ZDOTDIR/func $fpath)
if [[ "$OSTYPE" == darwin* ]] && command -v brew >/dev/null 2>&1; then
    export PATH="/usr/local/sbin:$PATH"
    export fpath=("$(brew --prefix)/share/zsh/site-functions" $fpath)
fi

# WSL: Add selective Windows binaries to PATH
if [[ -d /mnt/c/Windows ]]; then
    export PATH="$PATH:/mnt/c/Windows:/mnt/c/Windows/System32:/mnt/c/Program Files/PowerShell/7:/mnt/c/Users/lrabello/AppData/Local/Programs/Microsoft VS Code/bin:/mnt/c/Users/lrabello/AppData/Local/Programs/Microsoft VS Code Insiders/bin"
fi

# Ensure cache directory exists
[[ ! -d "$XDG_CACHE_HOME/zsh" ]] && mkdir -p "$XDG_CACHE_HOME/zsh"

# =============================================================================
# ZINIT INITIALIZATION
# =============================================================================

ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
if [[ ! -d "$ZINIT_HOME" ]]; then
    mkdir -p "$(dirname $ZINIT_HOME)"
    git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi

# Zinit optimization settings (must be set before sourcing zinit.zsh)
declare -A ZINIT
ZINIT[COMPINIT_OPTS]=-C
ZINIT[ZCOMPDUMP_PATH]="$XDG_CACHE_HOME/zsh/.zcompdump"

source "${ZINIT_HOME}/zinit.zsh"

# =============================================================================
# VI MODE (native zsh - fast alternative to zsh-vi-mode plugin)
# =============================================================================
bindkey -v
export KEYTIMEOUT=1

# Cursor shape changes for different vi modes
function zle-keymap-select {
    case $KEYMAP in
        vicmd) echo -ne '\e[2 q';;      # block cursor for normal mode
        viins|main) echo -ne '\e[6 q';; # beam cursor for insert mode
    esac
}
zle -N zle-keymap-select

# Ensure beam cursor on new prompts
function zle-line-init {
    echo -ne '\e[6 q'
}
zle -N zle-line-init

# =============================================================================
# PROMPT (immediate load)
# =============================================================================

# oh-my-posh
if (( $+commands[oh-my-posh] )); then
    eval "$(oh-my-posh init zsh --config ~/.config/oh-my-posh/theme.toml)"
fi

# =============================================================================
# SOURCE CONFIGURATION FILES (before turbo plugins)
# =============================================================================
# Source numbered config files in order using zinit snippets

zinit is-snippet for \
    "$ZDOTDIR"/[0-9]*.zsh

# =============================================================================
# TURBO-LOADED PLUGINS
# =============================================================================

# Batch A: Completions infrastructure (loads first)
zinit wait"0a" lucid for \
    blockf atpull'zinit creinstall -q .' \
        zsh-users/zsh-completions

# Batch B: fzf ecosystem + zoxide (needs completions available)
zinit wait"0b" lucid for \
    atload'source <(fzf --zsh)' \
        junegunn/fzf \
    Aloxaf/fzf-tab

# zoxide - load after fzf-tab for proper completion integration
# Create a ZLE widget for interactive zoxide, then bind it
zinit wait"0b" lucid has"zoxide" for \
    atload'
      eval "$(zoxide init --cmd cd zsh)"
      function _zoxide_cdi_widget() { __zoxide_zi; zle reset-prompt }
      zle -N _zoxide_cdi_widget
      bindkey "^o" _zoxide_cdi_widget
    ' \
        zdharma-continuum/null

# Batch C: Syntax highlighting + autosuggestions (load last, trigger compinit)
zinit wait"0c" lucid for \
    atinit"zicompinit; zicdreplay; compdef _dotnet_zsh_complete dotnet; compdef _eza ls" \
        zdharma-continuum/fast-syntax-highlighting \
    atload"_zsh_autosuggest_start" \
        zsh-users/zsh-autosuggestions

# =============================================================================
# LAZY-LOADED TOOLS (defer until needed)
# =============================================================================

# mise - lazy load, activates ~1s after prompt
zinit wait"1" lucid has"mise" for \
    atload'eval "$(mise activate zsh)"' \
        zdharma-continuum/null

# flux completion - lazy load ~2s after prompt
zinit wait"2" lucid has"flux" for \
    atload'eval "$(flux completion zsh)"' \
        zdharma-continuum/null
